# Advent of Code (AoC) - day 21, task 2

import "prelude.eg"

using System, OS, List, S = String, D = Dict

def dirs = ['<' -> (0,-1)|'>' -> (0,1)|'^' -> (-1,0)|'v' -> (1,0)|'A' -> (0,0)]

def to_keypad = do D::from_lists |> D::to_list |> map swap |> D::from_list |> flip D::erase ' '

val numeric = to_keypad {{'7','8','9'},{'4','5','6'},{'1','2','3'},{' ','0','A'}}
val digital = to_keypad {{' ','^','A'},{'<','v','>'}}

def buttons =
    [(0,0) -> {}
    |(0,Y) -> if Y < 0 then {'<'|buttons (0,Y+1)} else {'>'|buttons (0,Y - 1)}
    |(X,Y) -> if X < 0 then {'^'|buttons (X+1,Y)} else {'v'|buttons (X - 1,Y)}]

def presses =
    [D (N, T, A, {}) -> 0
    |D (N, T, A, {B|BB}) -> 
        let K = (if N==T then numeric else digital) in
        if N == 0 then
             let M = length (buttons (sub (D::get K B) (D::get K A))) + 1 in
             M + D::memo D presses (N, T, B, BB)
        else let PP = permutations (buttons (sub (D::get K B) (D::get K A))) |> unique |> map (flip (++) {'A'}) in
             let PP = filter [BB -> all (flip elem (D::values K)) (scanl add (D::get K A) (map dirs BB))] PP in
             let M = map [BB -> D::memo D presses (N - 1, T, 'A', BB)] PP |> minimum in
             M + D::memo D presses (N, T, B, BB) ]

def main =
    read_lines stdin |> map [X -> (to_int X, S::to_chars X)] 
    |> (let M = D::dict in map [(N, BB) -> (N, presses M (25, 25, 'A', BB))])
    |> map (uncurry (*)) |> sum

